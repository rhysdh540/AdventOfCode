//file:noinspection GrDeprecatedAPIUsage
allprojects {
    apply plugin: 'java'
    sourceSets.main.java {
        srcDir 'src'
    }
}

subprojects {
    project.ext.year = project.name.substring(4)
    dependencies {
        implementation(rootProject)
    }

    tasks.register('run', JavaExec) {
		group = 'aoc'
        classpath = sourceSets.main.runtimeClasspath
        mainClass = 'aoc.Main'
        argsString = project.ext.year
        workingDir = rootDir
        jvmArgs = '-Xmx8G -Xms8G -Xss1024m -XX:+UnlockExperimentalVMOptions -XX:+UseCriticalJavaThreadPriority -XX:+UseZGC -XX:AllocatePrefetchStyle=1 -XX:-ZProactive'.split(' ').toList()
    }

	compileJava {
		doFirst {
			print "Compiling ${project.name}..."
		}
		doLast {
			println " done!"
		}
	}

    tasks.register('initDay') {
		group = 'aoc'
        doLast {
            def day = null
			for(def i = 1; i <= 25; i++) {
				if(!project.file("src/Day${i}.java").exists()) {
					day = i
					break
				}
			}
			if(day == null) {
				throw new IllegalStateException("All days are already implemented!")
			}
            def source = project.file("src/Day${day}.java")
			source.parentFile.mkdirs()
			source.createNewFile()
			source.write("""import aoc.Day.IntDay;

import java.util.List;

/**
* <a href="https://adventofcode.com/${project.ext.year}/day/${day}">Day ${day}</a>
*/
public class Day${day} extends IntDay {
	@Override
	public int run1Int(List<String> input) {

	}

	@Override
	public int run2Int(List<String> input) {
		throw new UnsupportedOperationException();
	}
}
			""".stripIndent())
			project.file("input/${day}.txt").createNewFile()
        }
    }
}
